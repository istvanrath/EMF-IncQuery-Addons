package hu.bme.mit.incquery.cep.runtime.evaluation.queries

import "cep.meta"
import "sm.meta"
import "http://www.eclipse.org/emf/2002/Ecore"
uses hu.bme.mit.incquery.cep.runtime.evaluation.*

pattern finishedStateMachine(sm : StateMachine, cv:CurrentStateVisitor) {
	StateMachine.states(sm, state);
	find finalState(state, cv);
	CurrentStateVisitor.currentState(_, state);
}

pattern finalState(s : State, cv :CurrentStateVisitor) {
	State.label(s, label);
	State.currentVisitors(s, cv);
	check(label.equalsIgnoreCase("final"));
}

pattern latestEvent(e) {
	InternalExecutionModel.latestEvent(_, e);
}

pattern transition(t) {
	Transition(t);
}

pattern preState(t, s) {
	Transition.preState(t, s);
}

pattern eventHandledByCSV(e, csv) {
	CurrentStateVisitor.eventCollection(csv,e);
}

pattern enabledTransition(t : Transition, cv:CurrentStateVisitor) {
	find latestEvent(e);
	Event.typeId(e, eventId);
	Transition.guard.eventType(t, eventId);
	Transition.preState.currentVisitors(t, cv);
	neg find eventHandledByCSV(e, cv);
}
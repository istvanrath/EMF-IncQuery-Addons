package hu.bme.mit.incquery.cep.runtime.evaluation.queries

import "cep.meta"
import "sm.meta"
import "http://www.eclipse.org/emf/2002/Ecore"

pattern finishedStateMachine(sm : StateMachine, et : EventToken) {
	StateMachine.states(sm, state);
	find finalState(state, et);
	EventToken.currentState(_, state);
}

pattern finalState(s : State, et : EventToken) {
	State.label(s, label);
	State.eventTokens(s, et);
	check(label.equalsIgnoreCase("final"));
}

pattern latestEvent(e) {
	InternalExecutionModel.latestEvent(_, e);
}

pattern stateWithTimeconstraint(s : State, tc : TimeConstraint) {
	State.timeConstraints(s, tc);
}

pattern stateWithSTOPTimeconstraint(s : State, tc : TimeConstraint) {
	State.timeConstraints(s, tc);
	TimeConstraint.type(tc, ::STOP);
}

pattern stateWithSTARTTimeconstraint(s : State, tc : TimeConstraint) {
	State.timeConstraints(s, tc);
	TimeConstraint.type(tc, ::START);
}

pattern stateWithCHECKTimeconstraint(s : State, tc : TimeConstraint) {
	State.timeConstraints(s, tc);
	TimeConstraint.type(tc, ::CHECK);
}

pattern transition(t) {
	Transition(t);
}

pattern preState(t, s) {
	Transition.preState(t, s);
}

pattern guardAtomicPattern(guard : Guard, atomicPattern : AtomicEventPattern) {
	Guard.eventType(guard, atomicPattern);
}

pattern eventHandledByEventToken(et : EventToken, e : Event) {
	EventToken.recordedEvents(et, e);
}

pattern eventHandledByState(s : State, e : Event) {
	State.lastProcessedEvent(s, e);
}

pattern atomicPatternType(a : AtomicEventPattern, t : EString) {
	AtomicEventPattern.type(a, t);
}

pattern enabledTransition(t : Transition, et : EventToken) {
	find latestEvent(e);
	Event.type(e, eventType);
	Transition.guard.eventType.type(t, eventType);
	Transition.preState.eventTokens(t, et);
	Transition.preState(t, ps);
	neg find eventHandledByState(ps, e);
}

pattern tokenInTrapState(et : EventToken) {
	TrapState.eventTokens(_, et);
}